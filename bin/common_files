#!/usr/bin/env perl
use Modern::Perl;
use App::Framework;
use Digest::MD5;

our $VERSION = '2012.12';

sub app
{
    my ($app, $opts, $args) = @_ ;
    my %file_md5s;
    while(<STDIN>) {
        chomp;
        next unless -f $_;
        open(my $fh, '<', $_) or die "Can't open '$_': $!";
        binmode($fh);
        my $md5 = Digest::MD5->new->addfile($fh)->hexdigest;
        close($fh);
        $file_md5s{$md5} //= [];
        $_ =~ s/($opts->{separator})/\\$1/g;
        push @{$file_md5s{$md5}}, $_;
        print "$md5\t@{$file_md5s{$md5}}\n" if $opts->{debug}
    }
    for (keys %file_md5s) {
        if (@{$file_md5s{$_}} > 1) {
            print join($opts->{separator}, @{$file_md5s{$_}}), "\n"
                unless $opts->{unique_only}
        } else {
            print @{$file_md5s{$_}}, "\n"
                unless $opts->{multi_only}
        }
    }
}

go

__DATA__

[SUMMARY]

Find common files within the list of files provided in STDIN. Each output line
contains a list of files that are common, delimited with single spaces or an
alternate delimiter using the -s option.

[ARGS]

[OPTIONS]

-separator|s=s      Output record separator [default= ]
-unique_only|u      Only return unique files
-multi_only|m       Only return multiple common files

[DESCRIPTION]

B<$name> uses md5 checksums to compare files.
